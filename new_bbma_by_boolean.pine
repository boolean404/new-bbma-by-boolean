//@version=5

// start bb
indicator(shorttitle="NEW BBMA by Boolean", title="NEW BBMA by Boolean", overlay=true, timeframe="", timeframe_gaps=true)

mid_len = input.int(20, minval=1, title="Middle SMA", group="Bollinger Bands")
mid_src = input(close, title="Source", group="Bollinger Bands")
mult = input.float(2.0, minval=0.001, maxval=50, title="StdDev", group="Bollinger Bands")
bb_offset = input.int(0, "Offset", minval = -500, maxval = 500, group="Bollinger Bands")

basis = ta.sma(mid_src, mid_len)
dev = mult * ta.stdev(mid_src, mid_len)
upper_bb = basis + dev
lower_bb = basis - dev

plot(basis, "Middle", color=#2962FF, offset = bb_offset)
p1 = plot(upper_bb, "Upper", color=#2962FF, offset = bb_offset)
p2 = plot(lower_bb, "Lower", color=#2962FF, offset = bb_offset)
// fill(p1, p2, title = "Background", color=color.rgb(33, 150, 243, 95))
// end bb

// start moving average
wma1_len = input.int(5, minval=1, title="WMA 1", group="MA Length")
wma2_len = input.int(5, minval=1, title="WMA 2", group="MA Length")
wma3_len = input.int(10, minval=1, title="WMA 3", group="MA Length")
wma4_len = input.int(10, minval=1, title="WMA 4", group="MA Length")
ema_len = input.int(50, minval=1, title="EMA", group="MA Length")

wma1_src = input(low, title="WMA 1", group="MA Source")
wma2_src = input(high, title="WMA 2", group="MA Source")
wma3_src = input(low, title="WMA 3", group="MA Source")
wma4_src = input(high, title="WMA 4", group="MA Source")
ema_src = input(close, title="EMA", group="MA Source")

wma1_offset = input.int(title="WMA 1", defval=0, minval=-500, maxval=500, group="MA Offset")
wma2_offset = input.int(title="WMA 2", defval=0, minval=-500, maxval=500, group="MA Offset")
wma3_offset = input.int(title="WMA 3", defval=0, minval=-500, maxval=500, group="MA Offset")
wma4_offset = input.int(title="WMA 4", defval=0, minval=-500, maxval=500, group="MA Offset")
ema_offset = input.int(title="EMA", defval=0, minval=-500, maxval=500, group="MA Offset")

wma1_out = ta.wma(wma1_src, wma1_len)
wma2_out = ta.wma(wma2_src, wma2_len)
wma3_out = ta.wma(wma3_src, wma3_len)
wma4_out = ta.wma(wma4_src, wma4_len)
ema_out = ta.ema(ema_src, ema_len)

plot(wma1_out, title="WMA 1", color=#FF00FF, offset=wma1_offset)
plot(wma2_out, title="WMA 2", color=color.red, offset=wma2_offset)
plot(wma3_out, title="WMA 3", color=color.white, offset=wma3_offset)
plot(wma4_out, title="WMA 4", color=color.yellow, offset=wma4_offset)
plot(ema_out, color=color.aqua, title="EMA", offset=ema_offset)
// end moving average

// start extreme condition
// upper_extreme_condition = ta.crossover(wma2_out, upper_bb) and open[1] < close[1]
// super_upper_extreme_condition = ta.crossover(wma4_out, upper_bb)
// lower_extreme_condition = ta.crossunder(wma1_out, lower_bb) and open[1]  > close[1]
// super_lower_extreme_condition = ta.crossunder(wma3_out, lower_bb)

upper_extreme_condition = wma2_out > upper_bb and close < upper_bb
super_upper_extreme_condition =  wma4_out > upper_bb and close < upper_bb
lower_extreme_condition =  wma1_out < lower_bb and close > lower_bb
super_lower_extreme_condition = wma3_out < lower_bb and close > lower_bb

plotshape(series=upper_extreme_condition, location=location.abovebar, color=color.blue, style=shape.labeldown, title="Upper Extreme", size = size.small, text = "Extreme", textcolor=color.white)
plotshape(series=super_upper_extreme_condition, location=location.abovebar, color=color.blue, style=shape.labeldown, title="Super Upper Extreme", size = size.normal, text = "Super Extreme", textcolor=color.white)
plotshape(series=lower_extreme_condition, location=location.belowbar, color=color.blue, style=shape.labelup, title="Lower Extreme", size = size.small, text = "Extreme", textcolor=color.white)
plotshape(series=super_lower_extreme_condition, location=location.belowbar, color=color.blue, style=shape.labelup, title="Super Lower Extreme", size = size.normal, text = "Super Extreme", textcolor=color.white)
// end extreme condition

// start momentum condition
momentum_buy = close > upper_bb
momentum_sell = close < lower_bb

plotshape(series=momentum_buy, location=location.abovebar, color=color.green, style=shape.triangledown, title="CSM Buy", size = size.small, text = "CSM Buy", textcolor = color.green)
plotshape(series=momentum_sell, location=location.belowbar, color=color.red, style=shape.triangleup, title="CSM Sell", size = size.small, text = "CSM Sell", textcolor = color.red)
// endmomentum condition